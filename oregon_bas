   Print #2,"Loading: Oregon Trail"
1  REM ***********************************************
2  REM This is a adaptation of the original program to
3  REM be run in SuperBasic for Sinclair QL
5  REM by Alvaro Alea (2024, MAY 6)
6  REM by the way, I translate it to Spanish
7  REM ***********************************************
8  REM
10 REM PROGRAM NAME - OREGON        VERSION:01/01/78
20 REM ORIGINAL PROGRAMMING BY BILL HEINEMANN - 1971
30 REM SUPPORT RESEARCH AND MATERIALS BY DON RAVITSCH,
40 REM MINNESOTA EDUCATIONAL COMPUTING CONSORTIUM STAFF
50 REM CDC CYBER 70/73-26     BASIC 3.1
60 REM DOCUMENTATION BOOKLET 'OREGON' AVAILABLE FROM
61 REM    MECC SUPPORT SERVICES
62 REM    2520 BROADWAY DRIVE
63 REM    ST. PAUL MN 55113
80 REM
81 REM ========================================================
82 REM NOTE 1 = MODIFIED BY CHRISTOPHER PEDERSEN (AUG 10, 2018)
83 REM FOR COMPATIBILITY WITH THE CHIPMUNK BASIC INTERPRETER
84 REM CHIPMUNK BASIC: http://www.nicholson.com/rhn/basic/
85 REM ========================================================

90 REM ***IDENTIFICATION OF VARIABLES IN THE PROGRAM***
91 LET A% =0 : rem as Integer : REM = AMOUNT SPENT ON ANIMALS
92 LET B% =0 : rem  as Integer : REM  = AMOUNT SPENT ON AMMUNITION
93 REM LET B1 as Float : REM = ACTUAL RESPONSE TIME FOR INPUTTING "BANG"
94 REM LET B2 as Float : REM = CLOCK TIME AT END OF INPUTTING "BANG"
95 REM LET B3 as Float : REM = CLOCK TIME AT START OF INPUTTING "BANG"
96 LET C% =0 : rem  as Integer : REM  = AMOUNT SPENT ON CLOTHING
97 LET C1% =0: rem  as uByte : REM = FLAG FOR INSUFFICIENT CLOTHING IN COLD WEATHER
98 REM Z$ = YES/N0 RESPONSE TO QUESTIONS
99 LET D1% =0: rem  as uByte : REM = COUNTER IN GENERATING EVENTS
100 LET D3% =0: rem  as uByte : REM = TURN NUMBER FOR SETTING DATE
101 REM D4 = CURRENT DATE
102 LET D9% =0: rem  as uByte : REM = CHOICE OF SHOOTING EXPERTISE LEVEL
103 LET E% =0: rem  as uByte : REM = CHOICE OF EATING
104 LET F% =0 : rem  as Integer : REM  = AMOUNT SPENT ON FOOD
105 LET F1% =0: rem  as uByte : REM = FLAG FOR CLEARING SOUTH PASS
106 LET F2% =0: rem  as uByte : REM = FLAG FOR CLEARING BLUE MOUNTAINS
107 REM LET F9 as Float : REM = FRACTION OF 2 WEEKS TRAVELED ON FINAL TURN
108 LET K8% =0: rem  as uByte : REM = FLAG FOR INJURY
109 LET L1% =0: rem  as uByte : REM = FLAG FOR BLIZZARD
110 LET M% =0:  rem as uInteger : REM = TOTAL MILEAGE WHOLE TRIP
111 LET M1% =0 : rem  as Integer : REM  = AMOUNT SPENT ON MISCELLANEOUS SUPPLIES
112 LET M2% =0 : rem  as Integer : REM = TOTAL MILEAGE UP THROUGH PREVIOUS TURN
113 REM LET M9% =0: rem  as uByte : REM = FLAG FOR CLEARING SOUTH PASS IN SETTING MILEAGE
114 LET P% =0 : rem  as integer : REM = AMOUNT SPENT ON ITEMS AT FORT
115 REM LET R1 as float : REM = RANDOM NUMBER IN CHOOSING EVENTS
116 LET S4% =0: rem  as uByte : REM = FLAG FOR ILLNESS
117 LET S5% =0 : rem  as Integer : REM = ""HOSTILITY OF RIDERS"" FACTOR
118 LET S6% =0: rem  as uByte : REM = SHOOTING WORD SELECTOR
119 REM S$ = VARIATIONS OF SHOOTING WORD
120 LET T% =0 : rem  as Integer : REM  = CASH LEFT OVER AFTER INITIAL PURCHASES
121 LET T1% =0: rem  as uByte : REM = CHOICE OF TACTICS WHEN ATTACKED
122 LET X% =0: rem  as uByte : REM = CHOICE OF ACTION FOR EACH TURN
123 LET X1% =0: rem  as uByte : REM = FLAG FOR FORT OPTION155 REM
125 REM LET mes$ as String
126 REM LET temp1 as Float
127 REM LET temp2 as Float

130 mode 8
131 OPEN #1,con_512x256a0x0
132 rem WINDOW #1,256,256,0,0
150 INK 4: PAPER 0: CLS: pause 1 : cls
155 gosub 9000 : REMark Language Selection
160 PRINT S_NEED_INST$ ;
170 REM DIM C$(5)
180 REM RANDOMIZE REMOVED
190 INPUT Z$
200 IF (Z$="N") THEN GOTO 690
205    IF (Z$="n") THEN GOTO 690
210 PRINT
220 PRINT

230 REM ***INSTRUCTIONS********************
240 PRINT S_I_001$
245 PRINT S_I_002$
250 PRINT S_I_003$
252 PRINT S_I_004$
255 PRINT S_I_005$
260 PRINT S_I_006$
265 PRINT S_I_007$
270 PRINT S_I_008$
275 PRINT S_I_009$
280 PRINT
290 PRINT S_I_010$
300 PRINT S_I_011$
305 GOSUB 7000
310 PRINT S_I_012$
315 PRINT S_I_013$
320 PRINT S_I_014$
340 PRINT S_I_015$
345 PRINT S_I_016$
350 PRINT S_I_017$
360 PRINT S_I_018$
365 PRINT S_I_019$
380 PRINT S_I_020$
385 PRINT S_I_021$
390 PRINT S_I_022$
410 PRINT S_I_023$
415 PRINT S_I_024$
420 PRINT S_I_025$
425 PRINT S_I_026$
430 PRINT S_I_027$
450 PRINT S_I_028$
455 PRINT S_I_029$
460 PRINT S_I_030$
462 PRINT S_I_031$
465 PRINT S_I_032$
467 GOSUB 7000
490 PRINT S_I_033$
495 PRINT S_I_034$
500 PRINT S_I_035$
502 PRINT S_I_036$
505 PRINT S_I_037$
530 PRINT
540 PRINT S_I_038$
545 PRINT S_I_039$
550 PRINT S_I_040$
555 PRINT S_I_041$
560 PRINT S_I_042$
565 PRINT S_I_043$
570 PRINT S_I_044$
575 PRINT S_I_045$
580 PRINT S_I_046$
582 PRINT
585 GOSUB 7000
590 PRINT S_I_047$
595 PRINT S_I_048$
600 PRINT S_I_049$
605 PRINT S_I_050$
610 PRINT S_I_051$
615 PRINT S_I_052$
620 PRINT S_I_053$
625 PRINT S_I_054$
630 PRINT
635 GOSUB 7000
640 PRINT S_I_055$
640 PRINT S_I_056$
650 PRINT S_I_057$
660 PRINT S_I_058$
665 PRINT S_I_059$
670 PRINT
680 PRINT S_I_060$
685 GOSUB 7000

690 CLS
700 PRINT
710 PRINT S_ACE_01$
712 PRINT S_ACE_02$
715 PRINT
720 PRINT S_ACE_03$
722 PRINT S_ACE_04$
725 PRINT S_ACE_05$
730 PRINT S_ACE_06$
732 PRINT S_ACE_07$
735 PRINT
740 PRINT S_ACE_08$
745 PRINT S_ACE_09$
750 PRINT S_ACE_10$
755 PRINT S_ACE_11$
760 INPUT D9
770 IF D9>5 THEN GOTO 790
775 if D9<=1 then d9=1
780 GOTO 810
785 REM I think that is 5 not 0
790 D9=5
800 REM ***INITIAL PURCHASES***
810 X1=-1
820 REM LINES 820-829 NOTE 1
823 D3=0
824 M9=0
825 M=0
826 F2=0
827 F1=0
828 S4=0
829 K8=0
830 CLS
850 PRINT S_HOW_MUCH_OX$
860 INPUT A
870 IF A >= 200 THEN GOTO 900
880 PRINT S_NOT_ENOUGHT$
890 GOTO 850
900 IF A <= 300 THEN GOTO 930
910 PRINT S_TOO_MUCH$
920 GOTO 850
930 PRINT S_HOW_MUCH_FOOD$
940 INPUT F
950 IF F >= 0 THEN GOTO 980
960 PRINT S_IMPOSSIBLE$
970 GOTO 930
980 PRINT S_HOW_MUCH_BULLET$
990 INPUT B
1000 IF B >= 0 THEN GOTO 1030
1010 PRINT S_IMPOSSIBLE$
1020 GOTO 980
1030 PRINT S_HOW_MUCH_CLOTHES$
1040 INPUT C
1050 IF C >= 0 THEN GOTO 1080
1060 PRINT S_IMPOSSIBLE$
1070 GOTO 1030
1080 PRINT S_HOW_MUCH_OTHERS$
1090 INPUT M1
1100 IF M1 >= 0 THEN GOTO 1130
1110 PRINT S_IMPOSSIBLE$
1120 GOTO 1080
1130 T=700-A-F-B-C-M1
1140 IF T >= 0 THEN GOTO 1170
1150 PRINT S_SPEND_MUCH_1$
1152 PRINT S_SPEND_MUCH_2$
1155 PRINT S_SPEND_MUCH_3$
1157 GOSUB 7000
1160 GOTO 830
1170 B=50*B
1180 PRINT S_AFTER_BUY_1$
1182 PRINT S_AFTER_BUY_2$ ; T ;
1185 PRINT S_AFTER_BUY_3$
1187 GOSUB 7000
1190 PRINT
1200 PRINT S_FIRST_DATE$
1210 PRINT
1220 GOTO 1750

1230 IF M >= 2040 THEN GOTO 5430:end if
1240 REM ***SETTING DATE***
1250 D3=D3+1
1260 PRINT
1270 PRINT S_MONDAY$ ;
1280 REM IF D3>10 THEN GOTO 1300
1290 rem ON D3-1 GOTO 1310, 1330, 1350, 1370, 1390, 1410, 1430, 1450, 1470, 1490,   1510, 1530, 1550, 1570, 1590, 1610, 1630, 1650, 1670, 1690

1300 REM ON D3-10 GOTO 1510, 1530, 1550, 1570, 1590, 1610, 1630, 1650, 1670, 1690

1310 if D3=1 then PRINT S_04_12$ ; : goto 1720
1330 if D3=2 then PRINT S_04_26$ ; : GOTO 1720
1350 if D3=3 then PRINT S_05_10$ ; : GOTO 1720
1370 if D3=4 then PRINT S_05_24$ ; : GOTO 1720
1390 if D3=5 then PRINT S_06_07$ ; : GOTO 1720
1410 if D3=6 then PRINT S_06_21$ ; : GOTO 1720
1430 if D3=7 then PRINT S_07_05$ ; : GOTO 1720
1450 if D3=8 then PRINT S_07_19$ ; : GOTO 1720
1470 if D3=9 then PRINT S_08_02$ ; : GOTO 1720
1490 if D3=10 then PRINT S_08_16$ ; : GOTO 1720
1510 if D3=11 then PRINT S_08_31$ ; : GOTO 1720
1530 if D3=12 then PRINT S_09_13$ ; : GOTO 1720
1550 if D3=13 then PRINT S_09_27$ ; : GOTO 1720
1570 if D3=14 then PRINT S_10_11$ ; : GOTO 1720
1590 if D3=15 then PRINT S_10_25$ ; : GOTO 1720
1610 if D3=16 then PRINT S_11_08$ ; : GOTO 1720
1630 if D3=17 then PRINT S_11_22$ ; : GOTO 1720
1650 if D3=18 then PRINT S_12_06$ ; : GOTO 1720
1670 if D3=19 then PRINT S_12_20$ ; : GOTO 1720
1690 PRINT S_TRAIL_TOO_LONG$
1700 PRINT S_FAMILY_DIES$
1710 GOTO 5170
1720 PRINT S_YEAR$
1730 PRINT

1740 REM ***BEGINNING EACH TURN***

1750 IF F >= 0 THEN GOTO 1770
1760 F=0
1770 IF B >= 0 THEN GOTO 1790
1780 B=0
1790 IF C >= 0 THEN GOTO 1810
1800 C=0
1810 IF M1 >= 0 THEN GOTO 1830
1820 M1=0
1830 IF F >= 13 THEN GOTO 1850
1840 PRINT S_FOOD_LOW$
1850 F=INT(F)
1860 B=INT(B)
1870 C=INT(C)
1880 M1=INT(M1)
1890 T=INT(T)
1900 M=INT(M)
1910 M2=M
1920 IF S4=1 THEN GOTO 1950
1930 IF K8=1 THEN GOTO 1950
1940 GOTO 1990
1950 T=T-20
1960 IF T<0 THEN GOTO 5080
1970 PRINT S_BILL_DOCTOR$
1980 LET S4=0 : LET K8=S4
1990 IF M9=1 THEN GOTO 2020
2000 PRINT S_TOTAL_MILEAGE$ ;M
2010 GOTO 2040
2020 PRINT S_TOTAL_MILEAGE$ ; "950"
2030 M9=0
2040 REM LINES 2040-2050 NOTE 1
2041 PRINT S_FOOD$ ;F
2042 PRINT S_BULLETS$ ;B
2043 PRINT S_CLOTHING$ ;C
2044 PRINT S_MISC_SUPPLIES$ ;M1
2050 PRINT S_CASH$ ;T
2055 PRINT
2060 IF X1=-1 THEN GOTO 2160

2070 X1=X1*(-1)
2080 PRINT S_CHOOSE_12$ ;
2090 PRINT S_CHOOSE_3$
2100 INPUT X

2110 IF X>=3 THEN X=3 : GOTO 2270
2120 IF X<=1 THEN X=1 : GOTO 2270
2130 X=2 : GOTO 2230

2160 X1=X1*(-1)
2170 PRINT S_CHOOSE_23$
2180 INPUT X

2190 if X>=2 then x=3 : goto 2270
2220 X=2

2230 IF B>39 THEN GOTO 2270
2240 PRINT S_EJEM_BULLETS$
2250 X = 3

2270 rem CLS: REM ON X GOTO 2280,2530,2720
2271 SELECT ON X
2272 ON X=1
2273   GOTO 2280
2274 ON X=2
2275   GOTO 2530
2276 ON X=3
2277   GOTO 2720
2278 END SELECT

2280 PRINT S_AT_FORT$
2290 PRINT S_ENTER_HOW_MUCH$
2300 PRINT S_FOOD$ ;
2310 GOSUB 2330
2320 GOTO 2410
2330 INPUT P
2340 IF P<0 THEN GOTO 2400
2350 T=T-P
2360 IF T >= 0 THEN GOTO 2400
2370 PRINT S_DONT_HAVE_SUCH$
2380 T=T+P
2390 P=0
2400 RETURN
2410 F=F+2/3*P
2420 PRINT S_AMMUNITION$ ;
2430 GOSUB 2330
2440 LET B=INT(B+2/3*P*50)
2450 PRINT S_CLOTHING$ ;
2460 GOSUB 2330
2470 C=C+2/3*P
2480 PRINT S_MISC_SUPPLIES$ ;
2490 GOSUB 2330
2500 M1=M1+2/3*P
2510 M=M-45
2520 GOTO 2715

2530 PRINT 
2535 PRINT S_HUNTING$
2540 IF B>39 THEN GOTO 2570
2550 PRINT S_EJEM_BULLETS$
2560 GOTO 2080
2570 M=M-45
2580 GOSUB 6140
2590 IF B1 <= 1 THEN GOTO 2660
2595 REM FIXME: The next RND not seen to be good
2600 IF 100*RND<10*B1 THEN GOTO 2715: end if
2610 F=F+48-2*B1
2620 PRINT S_HUNT_1$
2630 B=B-10-3*B1
2640 GOTO 2715
2650 REM **BELLS IN LINE 2660**
2660 PRINT S_HUNT_2$
2670 PRINT S_HUNT_3$
2680 F=F+52+RND*6
2690 B=B-10-RND*4
2700 GOTO 2715
2710 PRINT S_HUNT_4$

2715 gosub 7000
2720 IF F >= 13 THEN GOTO 2740
2730 GOTO 5060
2740 PRINT 
2745 PRINT S_EAT1$
2750 PRINT S_EAT2$
2752 PRINT S_EAT3$
2755 PRINT S_EAT4$
2760 PRINT S_EAT5$
2770 INPUT E
2780 IF E>3 THEN GOTO 2750
2790 IF E<1 THEN GOTO 2750
2800 LET E=INT(E)
2810 LET F=F-8-5*E
2820 IF F >= 0 THEN GOTO 2860
2830 F=F+8+5*E
2840 PRINT S_EAT_MUCH$
2850 GOTO 2750
2860 LET M=M+200+(A-220)/5+10*RND
2870 REM LINES 2870-2872 NOTE 1
2871 C1=0
2872 L1=C1

2880 REM ***RIDERS ATTACK***

2889 REM LINE 2890 NOTE 1
2890 temp1=RND*10
2892 temp2= (((M/100)-4)^2+72) / (((M/100)-4)^2+12) -1
2895 IF temp1>temp2 THEN GOTO 3550
2900 PRINT S_RIDERS_01$ ;
2910 S5=0
2919 REM ALL RND(-1) FUNCTION CALLS HAVE BEEN CHANGED TO RND(1) BY C.D.P.
2920 IF RND<0.8 THEN GOTO 2950
2930 PRINT S_RIDERS_02$ ;
2940 S5=1
2950 PRINT S_RIDERS_03$
2960 PRINT S_RIDERS_04$
2970 PRINT S_RIDERS_05$
2980 IF RND>0.2 THEN GOTO 3000
2990 S5=1-S5
3000 INPUT T1
3010 IF T1<1 THEN GOTO 2970
3020 IF T1>4 THEN GOTO 2970
3030 T1=INT(T1)
3040 IF S5=1 THEN GOTO 3330
3050 IF T1>1 THEN GOTO 3110
3060 M=M+20
3070 M1=M1-15
3080 B=B-150
3090 A=A-40
3100 GOTO 3470
3110 IF T1>2 THEN GOTO 3240
3120 GOSUB 6140
3130 B=B-B1*40-80
3140 IF B1>1 THEN GOTO 3170
3150 PRINT S_RIDERS_11$
3160 GOTO 3470
3170 IF B1 <= 4 THEN GOTO 3220
3180 PRINT S_RIDERS_12$
3190 K8=1
3200 PRINT S_RIDERS_13$
3210 GOTO 3470
3220 PRINT S_RIDERS_14$
3230 GOTO 3470

3240 IF T1>3 THEN GOTO 3290
3250 IF RND>0.8 THEN GOTO 3450
3260 LET B=B-150
3270 M1=M1-15
3280 GOTO 3470
3290 GOSUB 6140
3300 B=B-B1*30-80
3310 M=M-25
3320 GOTO 3140
3330 IF T1>1 THEN GOTO 3370
3340 M=M+15
3350 A=A-10
3360 GOTO 3470
3370 IF T1>2 THEN GOTO 3410
3380 M=M-5
3390 B=B-100
3400 GOTO 3470
3410 IF T1>3 THEN GOTO 3430
3420 GOTO 3470
3430 M=M-20
3440 GOTO 3470
3450 PRINT S_RIDERS_21$
3460 GOTO 3550
3470 IF S5=0 THEN GOTO 3500
3480 PRINT S_RIDERS_22$
3490 GOTO 3550
3500 PRINT S_RIDERS_23$
3510 IF B >= 0 THEN GOTO 3550
3520 PRINT S_RIDERS_24$
3530 GOTO 5170

3540 REM ***SELECTION OF EVENTS***
3550 LET D1=0
3560 RESTORE
3570 R1=100*RND
3580 LET D1=D1+1
3590 IF D1=16 THEN GOTO 4670
3600 READ D
3610 IF R1>D THEN GOTO 3580
3620 DATA 6,11,13,15,17,22,32,35,37,42,44,54,64,69,95
3625 PRINT S_EVENT$
3630 REM IF D1>10 THEN GOTO 3650
3640 REM ON D1-1 GOTO 3660,3700,3740,3790,3820,3850,3880,3960,4130,4190
3641 if D1=0 then goto 3660
3642 if D1<=1 then goto 3700
3643 if D1<=2 then goto 3740
3644 if D1<=3 then goto 3790
3645 if D1<=4 then goto 3820
3646 if D1<=5 then goto 3850
3647 if D1<=6 then goto 3960
3648 if D1<=7 then goto 4130
3649 if D1<=8 then goto 4190
3650 REM ON D1-11 GOTO 4220,4290,4340,4560,4610,4670
3654 if D1<=9 then goto 4220
3655 if D1<=10 then goto 4290
3656 if D1<=11 then goto 4340
3657 if D1<=12 then goto 4560
3658 if D1<=13 then goto 4610
3659 goto 4670

3660 PRINT S_WAGON_BREAK$
3670 LET M=M-15-5*RND
3680 LET M1=M1-8
3690 GOTO 4710

3700 PRINT S_OX_INJURED$
3710 LET M=M-25
3720 LET A=A-20
3730 GOTO 4710

3740 PRINT S_BROKE_ARM$
3750 PRINT S_MAKE_SLING$
3760 M=M-5-4*RND
3770 M1=M1-2-3*RND
3780 GOTO 4710

3790 PRINT S_OX_WANDER$
3800 M=M-17
3810 GOTO 4710
3820 PRINT S_SON_LOST$
3830 M=M-10
3840 GOTO 4710

3850 PRINT S_UNSAFE_WATER$
3860 LET M=M-10*RND-2
3870 GOTO 4710

3880 IF M>950 THEN GOTO 4490
3890 PRINT S_HEAVY_RAINS$
3910 F=F-10
3920 B=B-500
3930 M1=M1-15
3940 M=M-10*RND-5
3950 GOTO 4710

3960 PRINT S_BANDITS_ATTACK$
3970 GOSUB 6140
3980 B=B-20*B1
3990 IF B >= 0 THEN GOTO 4030
4000 PRINT S_OUT_BULLETS$
4010 T=T/3
4020 GOTO 4040
4030 IF B1 <= 1 THEN GOTO 4100
4040 PRINT S_SHOT_LEG$
4050 K8=1
4060 PRINT S_BETTER_SEE_DOC$
4070 M1=M1-5
4080 A=A-20
4090 GOTO 4710

4100 PRINT S_QUICKY$
4110 PRINT S_GOT_EM$
4120 GOTO 4710

4130 PRINT S_WAGON_FIRE$
4140 F=F-40
4150 B=B-400
4160 LET M1=M1-RND*8-3
4170 M=M-15
4180 GOTO 4710

4190 PRINT S_LOSE_FOG$
4200 M=M-10-5*RND
4210 GOTO 4710

4220 PRINT S_KILL_SNAKE$
4230 B=B-10
4240 M1=M1-5
4250 IF M1 >= 0 THEN GOTO 4280
4260 PRINT S_SNAKE_KILL$
4270 GOTO 5170
4280 GOTO 4710

4290 PRINT S_WAGON_SWAMPED$
4300 F=F-30
4310 C=C-20
4320 M=M-20-20*RND
4330 GOTO 4710

4340 PRINT S_WILD_ANIMALS$
4350 GOSUB 6140
4360 IF B>39 THEN GOTO 4410
4370 PRINT S_LOW_BULLETS$
4380 PRINT S_WOLVE_OVER$
4390 K8=1
4400 GOTO 5120
4410 IF B1>2 THEN GOTO 4440
4420 PRINT S_NICE_SHOT$
4430 GOTO 4450
4440 PRINT S_SLOW_DRAW$
4450 B=B-20*B1
4460 C=C-B1*4
4470 F=F-B1*8
4480 GOTO 4710

4490 PRINT S_COLD_WEATHER$ ;
4500 IF C>22+4*RND THEN GOTO 4530
4510 PRINT S_DONT$ ;
4520 C1=1
4530 PRINT S_NO_CLOTHS$
4540 IF C1=0 THEN GOTO 4710
4550 GOTO 6300
4560 PRINT S_HAIL_STORM$
4570 M=M-5-RND*10
4580 B=B-200
4590 M1=M1-4-RND*3
4600 GOTO 4710

4610 IF E=1 THEN GOTO 6300
4620 IF E=3 THEN GOTO 4650
4630 IF RND>0.25 THEN GOTO 6300
4640 GOTO 4710
4650 IF RND<0.5 THEN GOTO 6300
4660 GOTO 4710
4670 PRINT S_HELPFUL_INDIANS$
4680 F=F+14
4690 GOTO 4710

4700 REM ***MOUNTAINS***
4710 GOSUB 7000
4712 IF M <= 950 THEN GOTO 1230
4719 REM LINE 4720 NOTE 1
4720 IF RND*10>9-((M/100-15)^2+72)/((M/100-15)^2+12) THEN GOTO 4860
4730 PRINT S_RUGGED_M$
4740 IF RND>0.1 THEN GOTO 4780
4750 PRINT S_LOST_TRAIL$
4760 M=M-60
4770 GOTO 4860
4780 IF RND>0.11 THEN GOTO 4840
4790 PRINT S_WAGON_DAMAGED$
4800 M1=M1-5
4810 B=B-200
4820 M=M-20-30*RND
4830 GOTO 4860
4840 PRINT S_GOING_SLOW$
4850 M=M-45-RND/0.02
4860 IF F1=1 THEN GOTO 4900
4870 F1=1
4880 IF RND<0.8 THEN GOTO 4970
4890 PRINT S_YOU_MADE$
4900 IF M<1700 THEN GOTO 4940
4910 IF F2=1 THEN GOTO 4940
4920 F2=1
4930 IF RND<0.7 THEN GOTO 4970
4940 IF M>950 THEN GOTO 1230
4950 M9=1
4960 GOTO 1230
4970 PRINT S_BLIZZARD$
4980 L1=1
4990 F=F-25
5000 M1=M1-10
5010 B=B-300
5020 M=M-30-40*RND
5030 IF C<18+2*RND THEN GOTO 6300
5040 GOTO 4940

5050 REM ***DYING***
5060 PRINT S_DIE_NO_FOOD$
5070 GOTO 5170
5080 LET T=0
5090 PRINT S_DIE_NO_DOCTOR$
5100 GOTO 5120
5110 PRINT S_DIE_NO_MEDICAL$
5120 PRINT S_DIE_YOU$ ;
5130 IF K8=1 THEN GOTO 5160
5140 PRINT S_DIE_ILL1$
5150 GOTO 5170
5160 PRINT S_DIE_INJURIES$
5170 PRINT
5175 GOSUB 7000
5180 PRINT S_DIE_01$
5190 PRINT S_DIE_02$
5200 PRINT
5210 PRINT S_DIE_03$
5220 INPUT Z$
5230 PRINT S_DIE_04$
5240 INPUT Z$
5250 PRINT S_DIE_05$
5260 INPUT Z$
5270 IF (Z$<>"no" AND Z$<>"NO" and z$<>"n" and z$<>"N") THEN GOTO 5310
5280 PRINT S_DIE_06$
5290 PRINT
5300 GOTO 5330
5310 PRINT S_DIE_07$
5320 GOSUB 7000
5330 PRINT S_DIE_08$
5340 PRINT S_DIE_09$
5350 PRINT S_DIE_10$
5360 PRINT
5370 PRINT
5380 PRINT S_DIE_11$
5390 PRINT
5400 PRINT S_DIE_12$
5409 REM 'STOP' COMMAND BELOW CHANGED TO 'END' BY C.D.P.
5410 GOTO 7100

5420 REM ***FINAL TURN***
5430 F9=(2040-M2)/(M-M2)
5440 F=F+(1-F9)*(8+5*E)
5450 PRINT
5460 REM **BELLS IN LINES 5470,5480**
5470 PRINT S_END1$
5480 PRINT S_END2$
5490 PRINT S_END3$
5500 PRINT
5510 F9=INT(F9*14)
5520 D3=D3*14+F9
5530 F9=F9+1
5540 IF F9<8 THEN GOTO 5560
5550 F9=F9-7
5560 ON F9 GOTO 5570,5590,5610,5630,5650,5670,5690
5570 PRINT S_CMONDAY$ ;
5580 GOTO 5700
5590 PRINT S_CTUESDAY$;
5600 GOTO 5700
5610 PRINT S_CWEDNESDAY$;
5620 GOTO 5700
5630 PRINT S_CTHRUSDAY$;
5640 GOTO 5700
5650 PRINT S_CFRIDAY$;
5660 GOTO 5700
5670 PRINT S_CSATURDAY$ ;
5680 GOTO 5700
5690 PRINT S_CSUNDAY$ ;
5700 IF D3>124 THEN GOTO 5740
5710 D3=D3-93
5720 LET mes$= S_CJULY$
5730 GOTO 5920
5740 IF D3>155 THEN GOTO 5780
5750 D3=D3-124
5760 LET mes$= S_CAUGUST$
5770 GOTO 5920
5780 IF D3>185 THEN GOTO 5820
5790 D3=D3-155
5800 LET mes$= S_CSEPTEMBER$
5810 GOTO 5920
5820 IF D3>216 THEN GOTO 5860
5830 D3=D3-185
5840 LET mes$= S_COCTOBER$
5850 GOTO 5920
5860 IF D3>246 THEN GOTO 5900
5870 D3=D3-216
5880 LET mes$= S_CNOVEMBER$
5890 GOTO 5920
5900 D3=D3-246
5910 LET mes$=S_CDECEMBER$
5920
5921 PRINT mes$; D3; S_YEAR$: REM English
5922 PRINT D3; mes$; S_YEAR$: REM Spanish
5925 PRINT
5930 REM LINE 5930 NOTE 1
5940 IF B>0 THEN GOTO 5960
5950 LET B=0
5960 IF C>0 THEN GOTO 5980
5970 LET C=0
5980 IF M1>0 THEN GOTO 6000
5990 LET M1=0
6000 IF T>0 THEN GOTO 6020
6010 LET T=0
6020 IF F>0 THEN GOTO 6040
6030 LET F=0
6040 REM LINES 6040-6045 NOTE 1
6041 PRINT S_FOOD$ ;F
6042 PRINT S_BULLETS$ ;B
6043 PRINT S_CLOTHING$ ;C
6044 PRINT S_MISC_SUPPLIES$ ;M1
6045 PRINT S_CASH$ ;T
6050 GOSUB 7000
6060 PRINT S_END_01$
6062 PRINT
6064 PRINT S_END_02$
6066 PRINT
6068 PRINT S_END_03$
6070 PRINT S_END_04$
6080 PRINT
6090 PRINT S_END_05$
6095 PRINT S_END_06$
6100 PRINT
6110 PRINT S_END_07$
6120 GOTO 7100

6130 REM ***SHOOTING SUB-ROUTINE***
6131 REM THE METHOD OF TIMING THE SHOOTING (LINES 6210-6240)
6132 REM WILL VARY FROM SYSTEM TO SYSTEM. FOR EXAMPLE, H-P
6133 REM USERS WILL PROBABLY PREFER TO USE THE 'ENTER' STATEMENT.
6134 REM IF TIMING ON THE USER'S SYSTEM IS HIGHLY SUSCEPTIBLE
6135 REM TO SYSTEM RESPONSE TIME, THE FORMULA IN LINE 6240 CAN
6136 REM BE TAILORED TO ACCOMMODATE THIS BY EITHER INCREASING
6137 REM OR DECREASING THE 'SHOOTING' TIME RECORDED BY THE SYSTEM
6140 DIM S$(4,4)
6150 S$(1)="bang"
6160 S$(2)="blam"
6170 S$(3)="pow"
6180 S$(4)="wham"
6182 B3 = date
6185 for a=1 to 4
6187   S6=INT(RND*4+0.5)
6188   if S6=0 then goto 6187
6190   PRINT S_TYPE$ ; S$(S6)
6192   INPUT Z$
6195   IF Z$<>S$(S6) THEN b3=b3-9
6197 next a
6200 B2 = date
6210 B1 = B2 - B3
6241 REM  /50 - (D9-1)
6242 rem PRINT "B3=";B3
6243 rem PRINT "B2=";B2
6244 rem PRINT "Seconds=";B1
6245 rem PRINT "DIFICULT=";D9
6246 B1= int (B1 - (D9*2))
6247 rem PRINT "Result=";B1
6248 rem GOSUB 7000
6250 PRINT
6255 IF B1>0 THEN GOTO 6260
6257 B1=0
6260 if B1>9 then B1=9
6280 RETURN
6282 REM so B1 = 0 => to fast
6285 REM    B1 = 9 => not correct
6287 REM    1 <= B1 <= 8 diferent quality.

6290 REM ***ILLNESS SUB-ROUTINE***
6300 IF 100*RND<10+35*(E-1) THEN GOTO 6370
6309 REM LINE 6310 NOTE 1
6310 IF 100*RND<100-(40/4^(E-1)) THEN GOTO 6410
6320 PRINT S_ILL_SERIOUS$
6330 PRINT S_ILL_MEDICAL$
6340 M1=M1-10
6350 S4=1
6360 GOTO 6440
6370 PRINT S_ILL_WILD$
6380 M=M-5
6390 M1=M1-2
6400 GOTO 6440
6410 PRINT S_ILL_BAD$
6420 M=M-5
6430 M1=M1-5
6440 IF M1<0 THEN GOTO 5110
6450 IF L1=1 THEN GOTO 4940
6460 GOTO 4710
6800 END

6900 REM Routines by Alea not in original

7000 PRINT
7010 PRINT S_PRESS_KEY$
7020 REPEAT wait
7021 IF INKEY$<>"" THEN EXIT wait
7022 REM WAIT Until the key isn't pressed any more.
7025 end rep wait
7030 PAUSE 0 : PAUSE 0
7040 CLS
7060 RETURN

7100 PRINT
7110 PRINT S_OTHER_PLAY$
7120 INPUT Z$
7130 if Z$<>"N" AND Z$<>"n" THEN GOTO 10
7140 NEW

9000 REM Language Selection
9010 print "Pulsa 1 para idioma Espanol"
9020 print "Press 2 for English Language"
9030 repeat x
9040   a$=inkey$
9050   if a$="1" then gosub 11000 : exit x
9060   if a$="2" then gosub 10000 : exit x
9070 end repeat x
9080 CLS
9090 return

10000 REM English Definitions.
10001 LET S_NEED_INST$ ="DO YO NEED INSTRUCTIONS(Y/N)"
10002 LET S_PRESS_KEY$ ="PRESS ANY KEY"

10003 LET S_I_001$ ="THIS PROGRAM SIMULATES A TRIP "
10004 LET S_I_002$ ="OVER THE OREGON TRAIL FROM"
10005 LET S_I_003$ ="INDEPENDENCE, MISSOURI TO"
10006 LET S_I_004$ ="OREGON CITY, OREGON IN"
10007 LET S_I_005$ ="1847."
10008 LET S_I_006$ ="YOUR FAMILY OF FIVE WILL COVER"
10009 LET S_I_007$ ="THE 2040 MILE OREGON TRAIL"
10010 LET S_I_008$ ="IN 5-6 MONTHS --- IF YOU MAKE"
10011 LET S_I_009$ ="IT ALIVE."
10012 LET S_I_010$ ="YOU HAD SAVED $900 TO SPEND FOR THE"
10013 LET S_I_011$ ="TRIP, AND YOU'VE JUST PAID $200 FOR A WAGON."

10014 LET S_I_012$ ="YOU WILL NEED TO SPEND THE REST"
10015 LET S_I_013$ ="OF YOUR MONEY ON THE FOLLOWING"
10016 LET S_I_014$ ="ITEMS:"
10017 LET S_I_015$ ="OXEN - YOU CAN SPEND $200-$300"
10018 LET S_I_016$ ="     ON YOUR TEAM, THE MORE YOU"
10019 LET S_I_017$ ="     SPEND,THE FASTER YOU'LL GO"
10020 LET S_I_018$ ="     BECAUSE YOU'LL HAVE BETTER"
10021 LET S_I_019$ ="     ANIMALS"
10022 LET S_I_020$ ="FOOD - THE MORE YOU HAVE, THE "
10023 LET S_I_021$ ="     LESS CHANCE THERE IS OF"
10024 LET S_I_022$ ="     GETTING SICK"
10025 LET S_I_023$ ="AMMUNITION - $1 BUYS A BELT OF"
10026 LET S_I_024$ ="     50 BULLETS, YOU WILL NEED"
10027 LET S_I_025$ ="     BULLETS FOR ATTACKS BY"
10028 LET S_I_026$ ="     ANIMALS AND BANDITS, AND"
10029 LET S_I_027$ ="     FOR HUNTING FOOD"
10030 LET S_I_028$ ="CLOTHING - THIS IS ESPECIALLY"
10031 LET S_I_029$ ="     IMPORTANT FOR THE COLD"
10032 LET S_I_030$ ="     WEATHER YOU WILL ENCOUNTER"
10033 LET S_I_031$ ="     WHEN CROSSING THE"
10034 LET S_I_032$ ="     MOUNTAINS"

10035 LET S_I_033$ ="MISCELLANEOUS SUPPLIES - THIS"
10036 LET S_I_034$ ="     INCLUDES MEDICINE AND"
10037 LET S_I_035$ ="     OTHER THINGS YOU WILL NEED"
10038 LET S_I_036$ ="     FOR SICKNESS AND EMERGENCY"
10039 LET S_I_037$ ="     REPAIRS"
10040 LET S_I_038$ ="YOU CAN SPEND ALL YOUR MONEY"
10041 LET S_I_039$ =" BEFORE YOU START YOUR TRIP -"
10042 LET S_I_040$ ="OR YOU CAN SAVE SOME OF YOUR"
10043 LET S_I_041$ =" CASH TO SPEND AT FORTS ALONG"
10044 LET S_I_042$ ="THE WAY WHEN YOU RUN LOW. "
10045 LET S_I_043$ ="HOWEVER, ITEMS COST MORE AT"
10046 LET S_I_044$ ="THE FORTS. YOU CAN ALSO GO"
10047 LET S_I_045$ =" HUNTING ALONG THE WAY TO GET"
10048 LET S_I_046$ ="MORE FOOD."

10049 LET S_I_047$ ="WHENEVER YOU HAVE TO USE YOUR"
10050 LET S_I_048$ ="TRUSTY RIFLE ALONG THE WAY,"
10051 LET S_I_049$ ="YOU WILL BE TOLD TO TYPE IN "
10052 LET S_I_050$ ="FOUR WORDS (THAT SOUNDS LIKE A "
10053 LET S_I_051$ ="GUN SHOT). THE FASTER YOU "
10054 LET S_I_052$ ="TYPE IN THE WORD AND HIT THE"
10055 LET S_I_053$ ="'RETURN' KEY, THE BETTER "
10056 LET S_I_054$ ="LUCK YOU'LL HAVE WITH YOUR GUN."

10057 LET S_I_055$ ="AT EACH TURN, ALL ITEMS ARE "
10058 LET S_I_056$ ="SHOWN IN DOLLAR AMOUNTS"
10059 LET S_I_057$ ="EXCEPT BULLETS"
10060 LET S_I_058$ ="WHEN ASKED TO ENTER MONEY "
10061 LET S_I_059$ ="AMOUNTS, DON'T USE A ''$."
10062 LET S_I_060$ ="GOOD LUCK!!!"

10063 LET S_ACE_01$ ="HOW GOOD A SHOT ARE YOU WITH "
10064 LET S_ACE_02$ ="YOUR RIFLE?"
10065 LET S_ACE_03$ ="  (1) ACE MARKSMAN"
10066 LET S_ACE_04$ ="  (2) GOOD SHOT"
10067 LET S_ACE_05$ ="  (3) FAIR TO MIDDLIN'"
10068 LET S_ACE_06$ ="  (4) NEED MORE PRACTICE"
10069 LET S_ACE_07$ ="  (5) SHAKY KNEES"
10070 LET S_ACE_08$ ="ENTER ONE OF THE ABOVE -- THE"
10071 LET S_ACE_09$ ="BETTER YOU CLAIM YOU ARE, THE"
10072 LET S_ACE_10$ ="FASTER YOU'LL HAVE TO BE WITH"
10073 LET S_ACE_11$ ="YOUR GUN TO BE SUCCESSFUL."

10074 LET S_HOW_MUCH_OX$ ="HOW MUCH DO YOU WANT TO SPEND ON YOUR OXEN TEAM"
10075 LET S_HOW_MUCH_FOOD$ ="HOW MUCH DO YOU WANT TO SPEND ON FOOD"
10076 LET S_HOW_MUCH_BULLET$ ="HOW MUCH DO YOU WANT TO SPEND ON AMMUNITION"
10077 LET S_HOW_MUCH_CLOTHES$ ="HOW MUCH DO YOU WANT TO SPEND ON CLOTHING"
10078 LET S_HOW_MUCH_OTHERS$ ="HOW MUCH DO YOU WANT TO SPEND ON MISCELLANEOUS SUPPLIES"
10079 LET S_NOT_ENOUGHT$ ="NOT ENOUGH"
10080 LET S_TOO_MUCH$ ="TOO MUCH"
10081 LET S_SPEND_MUCH_1$ ="YOU OVERSPENT--YOU "
10082 LET S_SPEND_MUCH_2$ ="ONLY HAD $700 TO SPEND."
10083 LET S_SPEND_MUCH_3$ ="BUY AGAIN"
10084 LET S_AFTER_BUY_1$ ="AFTER BUY"
10085 LET S_AFTER_BUY_2$ ="YOU NOW HAVE "
10086 LET S_AFTER_BUY_3$ =" DOLLARS LEFT"

10087 LET S_IMPOSSIBLE="IMPOSSIBLE"
10088 LET S_FOOD$ ="FOOD: "
10089 LET S_BULLETS$ ="BULLETS: "
10090 LET S_AMMUNITION$ ="AMMUNITION: "
10091 LET S_CLOTHING$ ="CLOTHING: "
10092 LET S_MISC_SUPPLIES$ ="MISC. SUPPLIES: "
10093 LET S_CASH$ ="CASH: $"
10094 LET S_TOTAL_MILEAGE$ ="TOTAL MILEAGE: "
10095 LET S_FOOD_LOW$ ="YOU'D BETTER DO SOME HUNTING OR BUY FOOD AND SOON!!!!"
10096 LET S_BILL_DOCTOR$ ="DOCTOR'S BILL IS $20"

10097 LET S_EAT1$ ="***EATING***"
10098 LET S_EAT2$ ="DO YOU WANT TO EAT:"
10099 LET S_EAT3$ ="(1) POORLY"
10100 LET S_EAT4$ ="(2) MODERATELY"
10101 LET S_EAT5$ ="(3) WELL"
10102 LET S_EAT_MUCH$ ="YOU CAN'T EAT THAT WELL"
10103 LET S_CHOOSE_23$ ="DO YOU WANT TO (1) HUNT, OR (2) CONTINUE"
10104 LET S_CHOOSE_12$ ="DO YOU WANT TO (1) STOP AT THE NEXT FORT, (2) HUNT, "
10105 LET S_CHOOSE_3$ ="OR (3) CONTINUE"
10106 LET S_TYPE$ ="TYPE "
10107 LET S_EJEM_BULLETS$ ="TOUGH --- YOU NEED MORE BULLETS TO GO HUNTING"
10108 LET S_AT_FORT$ ="***STOPPING AT FORT***"
10109 LET S_ENTER_HOW_MUCH$ ="ENTER WHAT YOU WISH TO SPEND ON THE FOLLOWING"
10110 LET S_DONT_HAVE_SUCH$ ="YOU DON'T HAVE THAT MUCH -- KEEP YOUR SPENDING DOWN"

10111 LET S_HUNTING$ ="***HUNTING***"
10112 LET S_HUNT_1$ ="NICE SHOT -- RIGHT ON TARGET--GOOD EATIN' TONIGHT!!"
10113 LET S_HUNT_2$ ="RIGHT BETWEEN THE EYES --- YOU GOT A BIG ONE!!!!"
10114 LET S_HUNT_3$ ="FULL BELLIES TONIGHT!"
10115 LET S_HUNT_4$ ="YOU MISSED --- AND YOUR DINNER GOT AWAY....."

10116 LET S_RIDERS_01$ ="RIDERS AHEAD. THEY "
10117 LET S_RIDERS_02$ ="DON'T "
10118 LET S_RIDERS_03$ ="LOOK HOSTILE"
10119 LET S_RIDERS_04$ ="TACTICS"
10120 LET S_RIDERS_05$ ="(1) RUN  (2) ATTACK  (3) CONTINUE  (4) CIRCLE WAGONS"
10121 LET S_RIDERS_11$ ="NICE SHOOTING --- YOU DROVE THEM OFF"
10122 LET S_RIDERS_12$ ="LOUSY SHOT---YOU GOT KNIFED"
10123 LET S_RIDERS_13$ ="YOU HAVE TO SEE OL' DOC BLANCHARD"
10124 LET S_RIDERS_14$ ="KINDA SLOW WITH YOUR COLT .45"
10125 LET S_RIDERS_21$ ="THEY DID NOT ATTACK"
10126 LET S_RIDERS_22$ ="RIDERS WERE FRIENDLY, BUT CHECK FOR POSSIBLE LOSSES"
10127 LET S_RIDERS_23$ ="RIDERS WERE HOSTILE -- CHECK FOR LOSES"
10128 LET S_RIDERS_24$ ="YOU RAN OUT OF BULLETS AND GOT MASSACRED BY THE RIDERS"

10129 LET S_DIE_NO_FOOD$ ="YOU RAN OUT OF FOOD AND STARVED TO DEATH"
10130 LET S_DIE_NO_DOCTOR$ ="YOU CAN'T AFFORD A DOCTOR"
10131 LET S_DIE_NO_MEDICAL$ ="YOU RAN OUT OF MEDICAL SUPPLIES"
10132 LET S_DIE_YOU$ ="YOU DIED OF "
10133 LET S_DIE_ILL1$ ="DYSENTERY"
10134 LET S_DIE_ILL2$ ="PNEUMONIA"
10135 LET S_DIE_INJURIES$ ="INJURIES"
10136 LET S_DIE_01$ ="DUE TO YOUR UNFORTUNATE SITUATION, THERE ARE A FEW"
10137 LET S_DIE_02$ ="FORMALITIES WE MUST GO THROUGH"
10138 LET S_DIE_03$ ="WOULD YOU LIKE A MINISTER?"
10139 LET S_DIE_04$ ="WOULD YOU LIKE A FANCY FUNERAL?"
10140 LET S_DIE_05$ ="WOULD YOU LIKE US TO INFORM YOUR NEXT OF KIN?"
10141 LET S_DIE_06$ ="BUT YOUR AUNT SADIE IN ST. LOUIS IS REALLY WORRIED ABOUT YOU"
10142 LET S_DIE_07$ ="THAT WILL BE $4.50 FOR THE TELEGRAPH CHARGE."
10143 LET S_DIE_08$ ="WE THANK YOU FOR THIS INFORMATION AND WE ARE SORRY YOU"
10144 LET S_DIE_09$ ="DIDN'T MAKE IT TO THE GREAT TERRITORY OF OREGON"
10145 LET S_DIE_10$ ="BETTER LUCK NEXT TIME"
10146 LET S_DIE_11$ ="SINCERELY"
10147 LET S_DIE_12$ ="THE OREGON CITY CHAMBER OF COMMERCE"

10148 LET S_FIRST_DATE$ ="Monday, March 29, 1847"
10149 LET S_MONDAY$ ="Monday, "
10150 LET S_04_12$ ="APRIL 12 "
10151 LET S_04_26$ ="APRIL 26 "
10152 LET S_05_10$ ="MAY 10 "
10153 LET S_05_24$ ="MAY 24 "
10154 LET S_06_07$ ="JUNE 7 "
10155 LET S_06_21$ ="JUNE 21 "
10156 LET S_07_05$ ="JULY 5 "
10157 LET S_07_19$ ="JULY 19 "
10158 LET S_08_02$ ="AUGUST 2 "
10159 LET S_08_16$ ="AUGUST 16 "
10160 LET S_08_31$ ="AUGUST 31 "
10161 LET S_09_13$ ="SEPTEMBER 13 "
10162 LET S_09_27$ ="SEPTEMBER 27 "
10163 LET S_10_11$ ="OCTOBER 11 "
10164 LET S_10_25$ ="OCTOBER 25 "
10165 LET S_11_08$ ="NOVEMBER 8 "
10166 LET S_11_22$ ="NOVEMBER 22 "
10167 LET S_12_06$ ="DECEMBER 6 "
10168 LET S_12_20$ ="DECEMBER 20 "
10169 LET S_YEAR$ ="1847"
10170 LET S_TRAIL_TOO_LONG$ ="YOU HAVE BEEN ON THE TRAIL TOO LONG  ------"
10171 LET S_FAMILY_DIES$ ="YOUR FAMILY DIES IN THE FIRST BLIZZARD OF WINTER"

10172 LET S_END1$ ="YOU FINALLY ARRIVED AT OREGON CITY"
10173 LET S_END2$ ="AFTER 2040 LONG MILES --- HOORAY!!!!!"
10174 LET S_END3$ ="A REAL PIONEER!"
10175 LET S_CMONDAY$ ="MONDAY "
10176 LET S_CTUESDAY$ ="TUESDAY "
10177 LET S_CWEDNESDAY$ ="WEDNESDAY "
10178 LET S_CTHRUSDAY$ ="THURSDAY "
10179 LET S_CFRIDAY$ ="FRIDAY "
10180 LET S_CSATURDAY$ ="SATURDAY "
10181 LET S_CSUNDAY$ ="SUNDAY "
10182 LET S_CJULY$ ="JULY "
10183 LET S_CAUGUST$ ="AUGUST "
10184 LET S_CSEPTEMBER$ ="SEPTEMBER "
10185 LET S_COCTOBER$ ="OCTOBER "
10186 LET S_CNOVEMBER$ ="NOVEMBER "
10187 LET S_CDECEMBER$ ="DECEMBER "

10188 LET S_EVENT$ =" *** EVENT ON THE WAY ***"
10189 LET S_WAGON_BREAK$ ="WAGON BREAKS DOWN -- LOSE TIME AND SUPPLIES FIXING IT"
10190 LET S_OX_INJURED$ ="OX INJURES LEG --- SLOWS YOU DOWN REST OF TRIP"
10191 LET S_BROKE_ARM$ ="BACK LUCK --- YOUR DAUGHTER BROKE HER ARM"
10192 LET S_MAKE_SLING$ ="YOU HAD TO STOP AND USE SUPPLIES TO MAKE A SLING"
10193 LET S_OX_WANDER$ ="OX WANDERS OFF --- SPEND TIME LOOKING FOR IT"
10194 LET S_SON_LOST$ ="YOUR SON GETS LOST --- SPEND HALF THE DAY LOOKING FOR HIM"
10195 LET S_UNSAFE_WATER$ ="UNSAFE WATER -- LOSE TIME LOOKING FOR CLEAN SPRING"
10196 LET S_HEAVY_RAINS$ ="HEAVY RAINS --- TIME AND SUPPLIES LOST"
10197 LET S_BANDITS_ATTACK$ ="BANDITS ATTACK"
10198 LET S_OUT_BULLETS$ ="YOU RAN OUT OF BULLETS --- THEY GET LOTS OF CASH"
10199 LET S_SHOT_LEG$ ="YOU GOT SHOT IN THE LEG AND THEY TOOK ONE OF YOUR OXEN"
10200 LET S_BETTER_SEE_DOC$ ="BETTER HAVE A DOC LOOK AT YOUR WOUND"
10201 LET S_QUICKY$ ="QUICKEST DRAW OUTSIDE OF DODGE CITY!!!"
10202 LET S_GOT_EM$ ="YOU GOT 'EM!"
10203 LET S_WAGON_FIRE$ ="THERE WAS A FIRE IN YOUR WAGON -- FOOD AND SUPPLIES DAMAGE!"
10204 LET S_LOSE_FOG$ ="LOSE YOUR WAY IN HEAVY FOG --- TIME IS LOST"
10205 LET S_KILL_SNAKE$ ="YOU KILLED A POISONOUS SNAKE AFTER IT BIT YOU"
10206 LET S_SNAKE_KILL$ ="YOU DIE OF SNAKEBITE SINCE YOU HAVE NO MEDICINE"
10207 LET S_WAGON_SWAMPED$ ="WAGON GETS SWAMPED FORDING RIVER -- LOSE FOOD AND CLOTHES"
10208 LET S_WILD_ANIMALS$ ="WILD ANIMALS ATTACK!"
10209 LET S_LOW_BULLETS$ ="YOU WERE TOO LOW ON BULLETS --"
10210 LET S_WOLVE_OVER$ ="THE WOLVES OVERPOWERED YOU"
10211 LET S_NICE_SHOT$ ="NICE SHOOTIN' PARTNER --- THEY DIDN'T GET MUCH"
10212 LET S_SLOW_DRAW$ ="SLOW ON THE DRAW --- THEY GOT AT YOUR FOOD AND CLOTHES"
10213 LET S_COLD_WEATHER$ ="COLD WEATHER --- BRRRRRRR! --- YOU "
10214 LET S_DONT$ ="DONT'T "
10215 LET S_NO_CLOTHS$ ="HAVE ENOUGH CLOTHING TO KEEP YOU WARM"
10216 LET S_HAIL_STORM$ ="HAIL STORM --- SUPPLIES DAMAGED"
10217 LET S_HELPFUL_INDIANS$ ="HELPFUL INDIANS SHOW YOU WHERE TO FIND MORE FOOD"


10218 LET S_RUGGED_M$ ="RUGGED MOUNTAINS"
10219 LET S_LOST_TRAIL$ ="YOU GOT LOST --- LOSE VALUABLE TIME TRYING TO FIND TRAIL!"
10220 LET S_WAGON_DAMAGED$ ="WAGON DAMAGED! --- LOSE TIME AND SUPPLIES"
10221 LET S_GOING_SLOW$ ="THE GOING GETS SLOW"
10222 LET S_YOU_MADE$ ="YOU MADE IT SAFELY THROUGH SOUTH PASS -- NO SNOW"
10223 LET S_BLIZZARD$ ="BLIZZARD IN MOUNTAIN PASS--TIME AND SUPPLIES LOST"

10224 LET S_ILL_SERIOUS$ ="SERIOUS ILLNESS---"
10225 LET S_ILL_MEDICAL$ ="YOU MUST STOP FOR MEDICAL ATTENTION"
10226 LET S_ILL_WILD$ ="WILD ILLNESS---MEDICINE USED"
10227 LET S_ILL_BAD$ ="BAD ILLNESS---MEDICINE USED"

10228 LET S_END_01$ ="      PRESIDENT"
10229 LET S_END_02$ ="    JAMES K. POLK"
10230 LET S_END_03$ ="    SENDS YOU HIS"
10231 LET S_END_04$ ="HEARTIEST CONGRATULATIONS"
10232 LET S_END_05$ ="   AND WISHES YOU A"
10233 LET S_END_06$ =" PROSPEROUS LIFE AHEAD"
10234 LET S_END_07$ ="   AT YOUR NEW HOME"
10235 LET S_OTHER_PLAY$ ="DO YOU WANT TO PLAY AGAIN?(Y/N)"

10236 RETURN

11000 REM Definiciones en espa√±ol
11001 LET S_NEED_INST$ ="NECESITAS INSTRUCCIONES (S/N)"
11002 LET S_PRESS_KEY$ ="PULSA UNA TECLA"

11003 LET S_I_001$ ="Este programa simula el viaje"
11004 LET S_I_002$ ="por la Ruta de Oregon, desde"
11005 LET S_I_003$ ="Indepencende, Missouri hasta"
11006 LET S_I_004$ ="la Ciudad de Oregon, Oregon, en"
11007 LET S_I_005$ ="1847."
11008 LET S_I_006$ ="Los cinco que formais la familia"
11009 LET S_I_007$ ="debeis cubrir las 2040 millas"
11010 LET S_I_008$ ="que forman la ruta de Oregon"
11011 LET S_I_009$ ="En 5-6 Meses --- Si llegais con vida..."
11012 LET S_I_010$ ="Has Ahorrado $900 para gastar en el viaje, "
11013 LET S_I_011$ ="y acabas de pagar $200 por una carreta."
11014 LET S_I_012$ ="Necesitas gastar el resto de"
11015 LET S_I_013$ ="tu dinero en alguno de los"
11016 LET S_I_014$ ="siguientes elementos:"
11017 LET S_I_015$ ="BUEYES - Debes gastar entre"
11018 LET S_I_016$ ="     $200-300 en el equipo,"
11019 LET S_I_017$ ="     a mas gastes, mas rapido"
11020 LET S_I_018$ ="     iras ya que tendras mejores"
11021 LET S_I_019$ ="     animales"
11022 LET S_I_020$ ="COMIDA - Cuanta mas tengas, "
11023 LET S_I_021$ ="     menores posibilidades de"
11024 LET S_I_022$ ="     caer enfermo"
11025 LET S_I_023$ ="MUNICION - $1 compra un cinturon"
11026 LET S_I_024$ ="     con 50 balas, las necesitas"
11027 LET S_I_025$ ="     contra los ataques de los"
11028 LET S_I_026$ ="     animales y bandidos y para"
11029 LET S_I_027$ ="     cazar comida"
11030 LET S_I_028$ ="ROPA - Es especialmente impor-"
11031 LET S_I_029$ ="     tante para combatir el frio"
11032 LET S_I_030$ ="     que te encontraras al"
11033 LET S_I_031$ ="     cruzar las montanas"
11034 LET S_I_032$ =""

11035 LET S_I_033$ ="SUMINISTROS VARIOS - Estos"
11036 LET S_I_034$ ="     incluyen medicinas y otras"
11037 LET S_I_035$ ="     cosas que necesitaras por"
11038 LET S_I_036$ ="     las enfermedades y para"
11039 LET S_I_037$ ="     reparaciones de emergencia"
11040 LET S_I_038$ ="Puedes gastar todo tu dinero"
11041 LET S_I_039$ ="antes de iniciar el viaje --"
11042 LET S_I_040$ ="O puedes guardar algo de dinero"
11043 LET S_I_041$ ="para gastar en los fuertes que"
11044 LET S_I_042$ ="encontraras en el camino. "
11045 LET S_I_043$ ="No obstante, las cosas son mas"
11046 LET S_I_044$ ="caras en los fuertes. Tambien"
11047 LET S_I_045$ ="puedes cazar por el camino para"
11048 LET S_I_046$ ="consegir mas comida."

11049 LET S_I_047$ ="Siempre que debas usar tu fiel"
11050 LET S_I_048$ ="rifle a lo largo del camino,"
11051 LET S_I_049$ ="se te pedira que teclees cuatro"
11052 LET S_I_050$ ="palabras (Parecidas al sonido"
11053 LET S_I_051$ ="de un disparo). A mas rapido"
11054 LET S_I_052$ ="las teclees y pulses 'ENTER'"
11055 LET S_I_053$ ="mayores seran tus posibilidades"
11056 LET S_I_054$ ="de acertar con el arma."

11057 LET S_I_055$ ="En cada turno, tu inventario "
11058 LET S_I_056$ ="se mostrara por el precio en $"
11059 LET S_I_057$ ="excepto las balas."
11060 LET S_I_058$ ="Cuando te pregunte por una"
11061 LET S_I_059$ ="Cantidad de dinero no uses el simbolo '$'"
11062 LET S_I_060$ ="BUENA SUERTE!!!"

11063 LET S_ACE_01$ ="COMO DE BUENO ERES DISPARANDO"
11064 LET S_ACE_02$ ="CON UN RIFLE?"
11065 LET S_ACE_03$ ="  (1) AS DE LA DIANA"
11066 LET S_ACE_04$ ="  (2) BUEN TIRADOR"
11067 LET S_ACE_05$ ="  (3) POR ENCIMA DE LA MEDIA"
11068 LET S_ACE_06$ ="  (4) NECESITO PRACTICAR"
11069 LET S_ACE_07$ ="  (5) ME TIEMBLAN LAS RODILLAS"
11070 LET S_ACE_08$ ="ELIJE ENTRE UNO DE ESTOS --"
11071 LET S_ACE_09$ ="CUANTO MEJOR DIGAS QUE ERES"
11072 LET S_ACE_10$ ="MAS RAPIDO TENDRAS QUE SER CON"
11073 LET S_ACE_11$ ="TU ARMA PARA SALIR ADELANTE"

11074 LET S_HOW_MUCH_OX$ ="Cuanto vas a gastar en el grupo de bueyes:"
11075 LET S_HOW_MUCH_FOOD$ ="Cuanto vas a gastar en comida:"
11076 LET S_HOW_MUCH_BULLET$ ="Cuanto vas a gastar en municion:"
11077 LET S_HOW_MUCH_CLOTHES$ ="Cuanto vas a gastar en ropa:"
11078 LET S_HOW_MUCH_OTHERS$ ="Cuanto vas a gastar en el resto de cosas:"
11079 LET S_NOT_ENOUGHT$ ="No es suficiente"
11080 LET S_TOO_MUCH$ ="Es demasiado"
11081 LET S_SPEND_MUCH_1$ ="TE HAS PASADO GASTANTO"
11082 LET S_SPEND_MUCH_2$ ="Solo tienes $700 para"
11083 LET S_SPEND_MUCH_3$ ="gastar, prueba otra vez."
11084 LET S_AFTER_BUY_1$ ="Despues de las compras"
11085 LET S_AFTER_BUY_2$ ="te quedan $"
11086 LET S_AFTER_BUY_3$ =""

11087 LET S_IMPOSSIBLE$ ="IMPOSIBLE"
11088 LET S_FOOD$ ="COMIDA: "
11089 LET S_BULLETS$ ="BALAS: "
11090 LET S_AMMUNITION$ ="MUNICION: "
11091 LET S_CLOTHING$ ="ROPA: "
11092 LET S_MISC_SUPPLIES$ ="EQUIPOS VARIOS: "
11093 LET S_CASH$ ="DINERO: $"
11094 LET S_TOTAL_MILEAGE$ ="DISTANCIA RECORRIDA: "
11095 LET S_FOOD_LOW$ ="DEBERIAS CAZAR O COMPRAR COMIDA PRONTO!!!!"
11096 LET S_BILL_DOCTOR$ ="La factura del Doctor son $20"

11097 LET S_EAT1$ ="***COMIENDO***"
11098 LET S_EAT2$ ="Como quieres comer:"
11099 LET S_EAT3$ ="(1) Pobremente"
11100LET S_EAT4$ ="(2) Moderadamente"
11101 LET S_EAT5$ ="(3) Copiosamente"
11102 LET S_EAT_MUCH$ ="NO PUEDES COMER TANTO"
11103 LET S_CHOOSE_23$ ="Quieres: (1) Cazar, o (2) Seguir"
11104 LET S_CHOOSE_12$ ="Quieres: (1) Parar en el siguiente fuerte,"
11105 LET S_CHOOSE_3$ ="(2) Cazar, o (3) Seguir"
11106 LET S_TYPE$ ="TECLEA: "
11107 LET S_EJEM_BULLETS$ ="EJEM -- Necesitas balas para ir de caza"
11108 LET S_AT_FORT$ ="*** PARANDO EN EL FUERTE ***"
11109 LET S_ENTER_HOW_MUCH$ ="INTRODUCE CUANTO QUIRERES GASTAR EN LO SIGUIENTE:"
11110 LET S_DONT_HAVE_SUCH$ ="NO TIENES TANTO -- Manten tus compras por debajo"

11111 LET S_HUNTING$ ="*** DE CAZA ***"
11112 LET S_HUNT_1$ ="BUEN DISPARO - JUSTO EN EL BLANCO - Esta noche habra buena cena!"
11113 LET S_HUNT_2$ ="JUSTO ENTRE LOS OJOS - HAS PILLADO UNO GRANDE!!"
11114 LET S_HUNT_3$ ="Esta noche las barrigas estaran a rebentar"
11115 LET S_HUNT_4$ ="HAS FALLADO -- Tu cena ha escapado..."

11116 LET S_RIDERS_01$ ="APARECEN BANDIDOS. Ellos"
11117 LET S_RIDERS_02$ =" no"
11118 LET S_RIDERS_03$ =" parecen hostiles"
11119 LET S_RIDERS_04$ ="TACTICAS"
11120 LET S_RIDERS_05$ ="(1) Correr (2) Atacar (3) Continuar (4) Circulo de Carretas"
11121 LET S_RIDERS_11$ ="BUEN DISPARO -- Te defiendes"
11122 LET S_RIDERS_12$ ="SONOROS DISPARO -- Te han dado"
11123 LET S_RIDERS_13$ ="Deberias ir a ver al viejo doctor Blanchard"
11124 LET S_RIDERS_14$ ="Demasiado lento con tu COLT .45"
11125 LET S_RIDERS_21$ ="Ellos no atacan"
11126 LET S_RIDERS_22$ ="Los bandidos parecen amables, pero comprueba tus cosas"
11127 LET S_RIDERS_23$ ="Los bandidos fueron hostiles -- comprueba las perdidas"
11128 LET S_RIDERS_24$ ="Te quedaste sin balas y los bandidos te masacran"

11129 LET S_DIE_NO_FOOD$ ="TE HAS QUEDADO SIN COMIDA Y MUERES DE HAMBRE"
11130 LET S_DIE_NO_DOCTOR$ ="NO TE PUEDES PERMITIR UN DOCTOR"
11131 LET S_DIE_NO_MEDICAL$ ="TE HAS QUEDADO SIN SUMINISTROS MEDICOS"
11132 LET S_DIE_YOU$ ="Mueres de "
11133 LET S_DIE_ILL1$ ="Disenteria"
11134 LET S_DIE_ILL2$ ="Neumonia"
11135 LET S_DIE_INJURIES$ ="las heridas"
11136 LET S_DIE_01$ ="Debido a esta desafortunada situacion,"
11137 LET S_DIE_02$ ="hay una serie de formalidades por las que debemos pasar:"
11138 LET S_DIE_03$ ="Desea un sacerdote?"
11139 LET S_DIE_04$ ="Desea un funeral pomposo?"
11140 LET S_DIE_05$ ="Desaa que avisemos a su familiar mas cercano?"
11141 LET S_DIE_06$ ="Pero su tia Sadie en St. Louis esta realmente preocupada por usted"
11142 LET S_DIE_07$ ="El coste del telegrama son $4.50"
11143 LET S_DIE_08$ ="Le agradecemos la informacion y le acompanamos en el sentimiento"
11144 LET S_DIE_09$ ="No ha conseguido alcanzar el gran territorio de Oregon"
11145 LET S_DIE_10$ ="La proxima vez tendra mejor suerte."
11146 LET S_DIE_11$ ="Sinceramente:"
11147 LET S_DIE_12$ ="La camara de comercio de Oregon"

11148 LET S_FIRST_DATE$ ="Lunes, 29 de Marzo de 1847"
11149 LET S_MONDAY$ ="Lunes, "
11150 LET S_04_12$ ="12 de Abril de "
11151 LET S_04_26$ ="26 de Abril de "
11152 LET S_05_10$ ="10 de Mayo de "
11153 LET S_05_24$ ="24 de Mayo de "
11154 LET S_06_07$ ="7 de Junio de "
11155 LET S_06_21$ ="21 de Junio de "
11156 LET S_07_05$ ="5 de Julio de "
11157 LET S_07_19$ ="19 de Julio de "
11158 LET S_08_02$ ="2 de Agosto de "
11159 LET S_08_16$ ="16 de Agosto de "
11160 LET S_08_31$ ="31 de Agosto de "
11161 LET S_09_13$ ="13 de Septiembre de "
11162 LET S_09_27$ ="27 de Setiembre de "
11163 LET S_10_11$ ="11 de Octubre de "
11164 LET S_10_25$ ="25 de Octubre de "
11165 LET S_11_08$ ="8 de Noviembre de "
11166 LET S_11_22$ ="22 de Noviembre de "
11167 LET S_12_06$ ="6 de Diciembre de "
11168 LET S_12_20$ ="20 de Diciembre de "
11169 LET S_YEAR$ ="1847"
11170 LET S_TRAIL_TOO_LONG$ ="HAS ESTADO DEMASIADO TIEMPO EN EL CAMINO  ------"
11171 LET S_FAMILY_DIES$ ="TU FAMILIA MUERE EN LA PRIMER TORMENTA DE GRANIZO DEL INVIERNO"

11172 LET S_END1$ ="HAS LLEGADO FINALMENTE A LA CIUDAD DE OREGON"
11173 LET S_END2$ ="TRAS 2040 MILLAS --- HURRA!!!!!"
11174 LET S_END3$ ="UN VERDADERO PIONERO!"
11175 LET S_CMONDAY$ ="LUNES, "
11176 LET S_CTUESDAY$ ="MARTES, "
11177 LET S_CWEDNESDAY$ ="MIERCOLES, "
11178 LET S_CTHRUSDAY$ ="JUEVES, "
11179 LET S_CFRIDAY$ ="VIERNES "
11180 LET S_CSATURDAY$ ="SABADO "
11181 LET S_CSUNDAY$ ="DOMINGO "
11182 LET S_CJULY$ =" DE JULIO DE "
11183 LET S_CAUGUST$ =" DE AUGOSTO DE "
11184 LET S_CSEPTEMBER$ =" DE SEPTIEMBRE DE "
11185 LET S_COCTOBER$ =" DE OCTUBRR DE"
11186 LET S_CNOVEMBER$ =" DE NOVIEMBRE DE"
11187 LET S_CDECEMBER$ =" DE DICIEMBRE DE"

11188 LET S_EVENT$ =" *** EVENTOS POR EL CAMINO ***"
11189 LET S_WAGON_BREAK$ ="EL CARROMATO SE ROMPE -- Pierdes tiempo y suministros arreglandolo"
11190 LET S_OX_INJURED$ ="UN BUEY SE HIERE EN UNA PATA -- Esto te retrasara el resto del viaje"
11191 LET S_BROKE_ARM$ ="MALA SUERTE -- Tu hija se rompe un brazo"
11192 LET S_MAKE_SLING$ ="Tienes que parar y gastar suministros en hacerle un cabestrillo"
11193 LET S_OX_WANDER$ ="UN BUEY SE ESCAPA -- Pierdes tiempo buscandolo"
11194 LET S_SON_LOST$ ="TU HIJO SE PIERDE -- Pierdes medio dia buscandolo"
11195 LET S_UNSAFE_WATER$ ="AGUA CONTAMINADA -- Pierdes tiempo buscando una fuente limpia"
11196 LET S_HEAVY_RAINS$ ="FUERTES LLUVIAS -- Pierdes tiempo y suministros"
11197 LET S_BANDITS_ATTACK$ ="ATAQUE DE BANDIDOS"
11198 LET S_OUT_BULLETS$ ="TE QUEDASS SIN BALAS -- Te roban un monton de dinero"
11199 LET S_SHOT_LEG$ ="Te pegan un tiro en la pierna y se llevan uno de tus bueyes"
11200 LET S_BETTER_SEE_DOC$ ="Seria conveniente que un doctor le echase un vistazo a esa herida"
11201 LET S_QUICKY$ ="EL MAS RAPIDO AL OESTE DE PECOS!"
11202 LET S_GOT_EM$ ="LES HAS DADO!"
11203 LET S_WAGON_FIRE$ ="FUEGO EN EL CARROMATO -- Se danan comida y suministros!"
11204 LET S_LOSE_FOG$ ="TE PIERDES EN LA NIEBLA -- Pierdes tiempo"
11205 LET S_KILL_SNAKE$ ="Matas una serpiente venenosa despues de que te mordiera"
11206 LET S_SNAKE_KILL$ ="Mueres por el mordisco, no tenias medicinas"
11207 LET S_WAGON_SWAMPED$ ="CARROMATO ATASCADO AL FONDEAR UN RIO -- Pierdes comida y suministros"
11208 LET S_WILD_ANIMALS$ ="ATAQUE DE ANIMALES SALVAJES!"
11209 LET S_LOW_BULLETS$ ="NO TIENES BALAS SUFICIENTES --"
11210 LET S_WOLVE_OVER$ ="Los Lobos te superan"
11211 LET S_NICE_SHOT$ ="BUEN DISPARO COMPANERO -- No han conseguido mucho"
11212 LET S_SLOW_DRAW$ ="MUY LENTO -- Se han llevado ropa y comida"
11213 LET S_COLD_WEATHER$ ="TIEMPO FRIO --- BRRRRRRR! --- Tu "
11214 LET S_DONT$ ="no "
11215 LET S_NO_CLOTHS$ ="tienes ropa suficiente para calentarte"
11216 LET S_HAIL_STORM$ ="TORMENTA DE GRANIZO -- Suministros danados"
11217 LET S_HELPFUL_INDIANS$ ="Unos Amables Indios te muestran donde encontrar mas comida"

11218 LET S_RUGGED_M$ ="MONTANAS ROCOSAS"
11219 LET S_LOST_TRAIL$ ="TE HAS PERDIDO -- Pierdes un tiempo precioso tratando de encontrar el canimo!"
11220 LET S_WAGON_DAMAGED$ ="CARROMATO DANADO! -- Pierdes tiempo y suministros"
11221 LET S_GOING_SLOW$ ="Estas yendo muy lento"
11222 LET S_YOU_MADE$ ="HAS CONSEGUIDO ATRAVESAR EL PASO DEL SUR -- No hay nieve"
11223 LET S_BLIZZARD$ ="TORMENTA DE NIEVE EN EL PASO DE LAS MONTANAS -- Tiempo y suministros perdidos"

11224 LET S_ILL_SERIOUS$ ="ENFERMEDAD SERIA --"
11225 LET S_ILL_MEDICAL$ ="Debes parar por atencion medica"
11226 LET S_ILL_WILD$ ="ENFERMEDAD SALVAJE --- Usaste Medicinas"
11227 LET S_ILL_BAD$ ="ENFERMEDAD CHUNGA --- Usaste Medicinas"

11228 LET S_END_01$ ="        EL PRESIDENTE"
11229 LET S_END_02$ ="        James K. Polk"
11230 LET S_END_03$ ="       Le envia sus mas"
11231 LET S_END_04$ ="  Fervientes Felicitaciones"
11232 LET S_END_05$ ="       y le desea una"
11233 LET S_END_06$ ="  prospera vida en adelante"
11234 LET S_END_07$ ="      EN SU NUEVO HOGAR"
11235 LET S_OTHER_PLAY$ ="QUIERES VOLVER A JUGAR(S/N)?"

11236 RETURN
      RUN














